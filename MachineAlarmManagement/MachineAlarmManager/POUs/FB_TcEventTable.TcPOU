<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.15">
  <POU Name="FB_TcEventTable" Id="{bd4c6123-962e-4d45-8be7-13868e45693b}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_TcEventTable
VAR CONSTANT
	EVENT_INDEX_TABLE_SIZE : UDINT := 80;
END_VAR
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR
	i : UDINT;
	init : BOOL;
	_queue : REFERENCE TO FB_TcEventQueue;
	_viewer : InterfaceEventViewer;
	_metadata : REFERENCE TO EventQueueCommand;
	_p_alarm	: POINTER TO FB_TcAlarm;
	_event_class	: GUID;
	_unconfirmed_event_count : UDINT;
	_active_event_count : UDINT;
	_set_event_count	: UDINT;
	await : BOOL;
	event_max_index : UDINT := 0;
	event_table  : ARRAY [1..AlarmEventParam.ALARM_MAX_COUNT] OF EventContainer;
	active_event_table	: ARRAY [1..EVENT_INDEX_TABLE_SIZE] OF POINTER TO EventContainer;
	active_event_max_index	: UDINT := 1;

	target_active_index : UDINT := 1;
	target_event_index : UDINT := 1;	
	view_refresh_flag : BOOL;
	
	view_terminate : BOOL;

	current_event : REFERENCE TO FB_Alarm;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// Getting text asynchronous process
IF NOT await AND _queue.usage > 0 THEN
	_metadata REF= _queue.get();
	IF __ISVALIDREF(_metadata) THEN
		await := TRUE;
	END_IF
END_IF



IF await THEN
	target_active_index := get_active_event_by_id(GUID_TO_STRING(_event_class), _metadata.metadata.nEventId);
	IF target_active_index <> 0 THEN
		IF _metadata.event_text_processor.GetString(
				active_event_table[target_active_index]^.event_text,
				SIZEOF(active_event_table[target_active_index]^.event_text)
		) THEN
			_viewer.send_event(active_event_table[target_active_index]^);
			create_view();
			await := FALSE;
		END_IF
	END_IF
END_IF

// Continuous execution for each event
REPEAT
	current_event REF= iter();
	IF __ISVALIDREF(current_event) THEN
		current_event();
	END_IF
UNTIL 
	NOT __ISVALIDREF(current_event)
END_REPEAT

// Refresh current aram list view
IF view_refresh_flag THEN
	_unconfirmed_event_count := 0;
	_active_event_count := 0;
	_viewer.clear(); // Once clear whole of items.
	i := 1;
	REPEAT
		IF i <= active_event_max_index THEN
			IF active_event_table[i] <> 0 THEN
				view_terminate := _viewer.show(active_event_table[i]^);
				_active_event_count := _active_event_count + 1;
				IF active_event_table[i]^.event.eConfirmationState = TcEventConfirmationState.WaitForConfirmation THEN
					_unconfirmed_event_count := _unconfirmed_event_count + 1;
				END_IF
			END_IF
		END_IF
		i := i + 1;
	UNTIL
		i > active_event_max_index OR view_terminate
	END_REPEAT
	view_refresh_flag := FALSE;
END_IF

// Watch every time.
_set_event_count := 0;
i := 1;
REPEAT
	IF i <= active_event_max_index AND active_event_table[i] <> 0 THEN
		IF active_event_table[i]^.event.set_activate THEN
			_set_event_count := _set_event_count + 1;
		END_IF
	END_IF
	i := i + 1;
UNTIL
	i > active_event_max_index
END_REPEAT
]]></ST>
    </Implementation>
    <Property Name="active_event_count" Id="{049793ad-ac3c-4ec7-899e-ae9a025e61e2}">
      <Declaration><![CDATA[PROPERTY active_event_count : UDINT]]></Declaration>
      <Get Name="Get" Id="{61179c2e-cbe1-4df9-ac77-3688a57040f0}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[active_event_count := _active_event_count;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="add_event" Id="{e7ff2180-5faf-4016-9356-bfdf374ae396}">
      <Declaration><![CDATA[METHOD add_event : BOOL
VAR_INPUT
	fbEvent	: REFERENCE TO FB_TcEvent;
END_VAR
VAR
	active_index 	: UDINT;
	event_index 	: UDINT;
	first_null_index		: UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[event_index := get_event_by_event(fbEvent);
first_null_index := 0;
FOR active_index := 1 TO active_event_max_index DO
	IF active_event_table[active_index] = 0 THEN
		IF first_null_index = 0 THEN
			first_null_index := active_index;
		END_IF
	ELSIF ADR(event_table[event_index]) = active_event_table[active_index] THEN
		RETURN;
	END_IF
END_FOR

IF first_null_index = 0 THEN
	active_event_max_index := active_index;
ELSE
	active_index := first_null_index;
END_IF

active_event_table[active_index] := ADR(event_table[event_index]);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="confirm_all_alarm" Id="{265cbf69-5377-470a-b98a-e4ecab82db32}">
      <Declaration><![CDATA[METHOD confirm_all_alarm : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR i := 1 TO event_max_index DO
	event_table[i].event.set_confirm := TRUE;
END_FOR]]></ST>
      </Implementation>
    </Method>
    <Method Name="create_view" Id="{ed7d59f3-0266-4d5f-b7ba-d66b645660ad}">
      <Declaration><![CDATA[METHOD create_view : BOOL
VAR_INPUT
END_VAR
VAR
	i : UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _viewer <> 0 THEN
	view_refresh_flag := TRUE;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Property Name="event_class" Id="{b449029f-922c-4230-aa54-25bc9ed0a655}">
      <Declaration><![CDATA[PROPERTY event_class : GUID]]></Declaration>
      <Set Name="Set" Id="{eaab0d96-b6ed-4429-8635-fbf24db491a6}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_event_class := event_class;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="get_active_event_by_id" Id="{ebfe7c27-bb66-47b0-9800-a37150ef7e85}">
      <Declaration><![CDATA[METHOD get_active_event_by_id : UDINT
VAR_INPUT
	sEventClass	: STRING;
	nEventID	: UDINT;
END_VAR
VAR
	i : UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[get_active_event_by_id := 0;

FOR i := 1 TO active_event_max_index DO
	IF active_event_table[i] <> 0 THEN
		IF GUID_TO_STRING(active_event_table[i]^.event.EventClass) = sEventClass AND active_event_table[i]^.event.nEventId = nEventID THEN
			get_active_event_by_id := i;
			RETURN;
		END_IF		
	END_IF
END_FOR]]></ST>
      </Implementation>
    </Method>
    <Method Name="get_alarm" Id="{f48e8fb5-405b-4520-a76b-f421a00d86d2}">
      <Declaration><![CDATA[METHOD get_alarm : REFERENCE TO FB_Alarm
VAR_INPUT
	index : UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF index <= event_max_index THEN
	get_alarm REF= event_table[index].event;
ELSE
	get_alarm REF= 0;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="get_event_by_event" Id="{779c7688-04d2-4214-9f69-44af0ed36a67}">
      <Declaration><![CDATA[METHOD get_event_by_event : UDINT
VAR_INPUT
	event	: I_TcEventBase;
END_VAR
VAR
	i : UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[get_event_by_event := 0;

FOR i := 1 TO event_max_index DO
	IF event_table[i].event.EqualsTo(event) THEN
		get_event_by_event := i;
		RETURN;
	END_IF
END_FOR]]></ST>
      </Implementation>
    </Method>
    <Method Name="init_instance" Id="{4f4ba2bf-a3da-4a17-9512-097312ae5cb8}">
      <Declaration><![CDATA[METHOD init_instance : POINTER TO FB_Alarm
VAR_INPUT
	eventClass			: GUID; // イベントクラスのGUID
	nEventId			: UDINT; // 登録したイベントID
	eSeverity			: TcEventSeverity; // 重要度
	bWithConfirmation	: BOOL;
	bWithResetOperation	: BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF event_max_index >= AlarmEventParam.ALARM_MAX_COUNT THEN
	init_instance := 0;
	RETURN;
END_IF

event_max_index := event_max_index + 1;
event_table[event_max_index].event.Create(
			eventClass := eventClass,
			nEventId := nEventId,
			eSeverity := eSeverity,
			bWithConfirmation := bWithConfirmation,
			ipSourceInfo := 0
		);
event_table[event_max_index].event.bWithResetOperation := bWithResetOperation;

init_instance := ADR(event_table[event_max_index]);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="iter" Id="{3babe4d2-ac0f-4ebd-9eaf-0347807890a3}">
      <Declaration><![CDATA[METHOD iter : REFERENCE TO FB_Alarm
VAR_INPUT
END_VAR
VAR_INST
	last_index : UDINT := 1;
END_VAR
VAR
	i : UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF last_index > event_max_index THEN
	iter REF= 0;
	last_index := 1;
ELSE
	iter REF= event_table[last_index].event;
	last_index := last_index + 1;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Property Name="queue" Id="{1318f451-5c86-4076-adbd-eac869b2c5e2}">
      <Declaration><![CDATA[PROPERTY queue : REFERENCE TO FB_TcEventQueue]]></Declaration>
      <Set Name="Set" Id="{0f2c9bb9-cbda-49a7-9a17-6fb677036656}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_queue REF= queue;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="remove_event" Id="{37e5a57b-2786-429e-96cf-2cee9949088e}">
      <Declaration><![CDATA[METHOD remove_event : BOOL
VAR_INPUT
	fbEvent	: REFERENCE TO FB_TcEvent;
END_VAR
VAR
	active_index 	: UDINT;
	last_active_index	: UDINT;
	
END_VAR
VAR_INST
	not_found_error: BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[not_found_error := TRUE;
FOR active_index := 1 TO active_event_max_index DO
	IF active_event_table[active_index] <> 0 THEN
		IF active_event_table[active_index]^.event.EqualsTo(fbEvent) THEN
			_viewer.send_event(active_event_table[active_index]^);
			active_event_table[active_index] := 0;
			IF active_index = active_event_max_index THEN
				active_event_max_index := last_active_index;
			END_IF
		END_IF
		not_found_error := FALSE;
		last_active_index := active_index;
	END_IF
END_FOR

remove_event := NOT not_found_error;]]></ST>
      </Implementation>
    </Method>
    <Property Name="set_event_count" Id="{138a797f-c33c-476a-aec9-1ecc8fe922b9}">
      <Declaration><![CDATA[PROPERTY set_event_count : UDINT]]></Declaration>
      <Get Name="Get" Id="{46857bc3-1e53-4baf-9433-c35d03f3926a}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[set_event_count := _set_event_count;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="try_clear_all_alarm" Id="{45e7786e-4b31-4139-8bfe-1444f48a2341}">
      <Declaration><![CDATA[METHOD try_clear_all_alarm : BOOL
VAR_INPUT
END_VAR
VAR
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR i := 1 TO event_max_index DO
	event_table[i].event.set_clear := TRUE;
END_FOR]]></ST>
      </Implementation>
    </Method>
    <Property Name="unconfirmed_event_count" Id="{279ba5ae-8181-4d70-af33-a02bb2cd8529}">
      <Declaration><![CDATA[PROPERTY unconfirmed_event_count : UDINT]]></Declaration>
      <Get Name="Get" Id="{115a057d-34e0-4060-b94d-9a13dd9e40fe}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[unconfirmed_event_count := _unconfirmed_event_count;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="viewer" Id="{30122376-3fde-4c80-a11e-1309fe3260f0}">
      <Declaration><![CDATA[PROPERTY viewer : InterfaceEventViewer]]></Declaration>
      <Get Name="Get" Id="{abc345f7-e043-4713-a1da-1e1164f97abe}">
        <Declaration><![CDATA[
VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[viewer := _viewer;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{3a2a1b2d-3294-45ea-9b8d-316d7e52eeec}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_viewer := viewer;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <LineIds Name="FB_TcEventTable">
      <LineId Id="1157" Count="69" />
      <LineId Id="403" Count="0" />
    </LineIds>
    <LineIds Name="FB_TcEventTable.active_event_count.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_TcEventTable.add_event">
      <LineId Id="66" Count="2" />
      <LineId Id="75" Count="1" />
      <LineId Id="78" Count="1" />
      <LineId Id="77" Count="0" />
      <LineId Id="71" Count="1" />
      <LineId Id="70" Count="0" />
      <LineId Id="80" Count="2" />
      <LineId Id="84" Count="1" />
      <LineId Id="83" Count="0" />
      <LineId Id="65" Count="0" />
      <LineId Id="24" Count="0" />
      <LineId Id="29" Count="0" />
    </LineIds>
    <LineIds Name="FB_TcEventTable.confirm_all_alarm">
      <LineId Id="13" Count="1" />
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_TcEventTable.create_view">
      <LineId Id="27" Count="0" />
      <LineId Id="36" Count="0" />
      <LineId Id="29" Count="0" />
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_TcEventTable.event_class.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_TcEventTable.get_active_event_by_id">
      <LineId Id="19" Count="2" />
      <LineId Id="33" Count="0" />
      <LineId Id="36" Count="2" />
      <LineId Id="34" Count="1" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_TcEventTable.get_alarm">
      <LineId Id="5" Count="0" />
      <LineId Id="7" Count="0" />
      <LineId Id="9" Count="1" />
      <LineId Id="8" Count="0" />
    </LineIds>
    <LineIds Name="FB_TcEventTable.get_event_by_event">
      <LineId Id="16" Count="0" />
      <LineId Id="18" Count="0" />
      <LineId Id="11" Count="1" />
      <LineId Id="14" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="15" Count="0" />
    </LineIds>
    <LineIds Name="FB_TcEventTable.init_instance">
      <LineId Id="5" Count="0" />
      <LineId Id="26" Count="0" />
      <LineId Id="7" Count="1" />
      <LineId Id="35" Count="0" />
      <LineId Id="10" Count="1" />
      <LineId Id="48" Count="4" />
      <LineId Id="9" Count="0" />
      <LineId Id="61" Count="0" />
      <LineId Id="27" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="24" Count="0" />
    </LineIds>
    <LineIds Name="FB_TcEventTable.iter">
      <LineId Id="27" Count="0" />
      <LineId Id="30" Count="0" />
      <LineId Id="33" Count="0" />
      <LineId Id="28" Count="0" />
      <LineId Id="26" Count="0" />
      <LineId Id="34" Count="0" />
      <LineId Id="32" Count="0" />
      <LineId Id="36" Count="0" />
    </LineIds>
    <LineIds Name="FB_TcEventTable.queue.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_TcEventTable.remove_event">
      <LineId Id="32" Count="0" />
      <LineId Id="14" Count="1" />
      <LineId Id="17" Count="0" />
      <LineId Id="51" Count="0" />
      <LineId Id="20" Count="0" />
      <LineId Id="22" Count="2" />
      <LineId Id="21" Count="0" />
      <LineId Id="35" Count="0" />
      <LineId Id="19" Count="0" />
      <LineId Id="18" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="37" Count="0" />
      <LineId Id="39" Count="0" />
    </LineIds>
    <LineIds Name="FB_TcEventTable.set_event_count.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_TcEventTable.try_clear_all_alarm">
      <LineId Id="14" Count="1" />
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_TcEventTable.unconfirmed_event_count.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_TcEventTable.viewer.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_TcEventTable.viewer.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>